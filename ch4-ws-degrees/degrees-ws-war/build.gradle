apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse-wtp'
compileJava.options.encoding = 'UTF-8'

httpPort = 8081

[jettyRun, jettyRunWar,jettyStop]*.stopPort = 9966
[jettyRun, jettyRunWar,jettyStop]*.stopKey = 'stopKey'

repositories {
	mavenCentral()
}
	
dependencies {
	compile 'log4j:log4j:1.2.14'
  compile 'com.sun.xml.ws:jaxws-rt:2.2.8'
	testCompile 'junit:junit:4.+'
	providedCompile 'javax.servlet:servlet-api:2.5'
	compile project(':degrees-ws-jar')
}

test {
  exclude '**/*ITCase.*'
  //reports.html.enabled = false
  //setTestReport(false)
  reports.html.destination = file("$reports.html.destination/unit") 
  reports.junitXml.destination =file("$reports.junitXml.destination/unit") 
}

task integrationTest(type:Test) {
  include '**/*ITCase.*'
  reports.html.destination = file("$reports.html.destination/integration") 
  reports.junitXml.destination = file("$reports.junitXml.destination/integration") 
  
  doFirst {
    jettyRun.daemon = true
    jettyRun.execute()
  }
  doLast {
    jettyStop.execute()
  }
}

task testReport(type: TestReport){
  destinationDir = file("$buildDir/reports/allTests")
  reportOn test, integrationTest
}

check.dependsOn testReport
testReport.dependsOn integrationTest
integrationTest.dependsOn test
